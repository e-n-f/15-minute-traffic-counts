set terminal postscript
set xrange [0:24]
set yrange [0:0.15]

# cat ../../*/counts*csv | ./get-time-distrib2 -p | sort -n > distrib

print "gaussian_pdf(x, u, o) = exp(- (x - u) * (x - u) / (2 * o * o))/(o * sqrt(2 * pi))";
print "gaussian_cdf(x, u, o) = .5 * (.5 + erf((x - u) / (o * sqrt(2))))";

gaussian_pdf(x, u, o) = exp(- (x - u) * (x - u) / (2 * o * o))/(o * sqrt(2 * pi))
gaussian_cdf(x, u, o) = .5 * (.5 + erf((x - u) / (o * sqrt(2))))

f(x) = a * gaussian_pdf(x, u, o) + a1 * gaussian_pdf(x, u1, o1) + a2 * gaussian_pdf(x, u2, o2) + a3 * gaussian_pdf(x, u3, o3)
g(x) = a * gaussian_cdf(x, u, o) + a1 * gaussian_cdf(x, u1, o1) + a2 * gaussian_cdf(x, u2, o2) + a3 * gaussian_cdf(x, u3, o3)

a               = 0.607644
u               = 16.1794
o               = 4.88055
a1              = 0.162765
u1              = 8.34676
o1              = 1.30244
a2              = 0.0896906
u2              = 12.2895
o2              = 1.31262
a3              = 0.139982
u3              = 16.8618
o3              = 1.96718




fit f(x) + f(x + 24) + f(x - 24) "minn-1" using 1:2 via a, u, o, a1, u1, o1, a2, u2, o2, a3, u3, o3
plot "minn-1" using ($1 - 0.125):2 title "" with points ps .4, f(x) + f(x + 24) + f(x - 24) title "" lw 5

scale = 1 / (g(24) + g(24 + 24) + g(24 - 24) - (g(0) + g(24) + g(-24)))
a = a * scale
a1 = a1 * scale
a2 = a2 * scale
a3 = a3 * scale

print "walk(x) =    ";
print "             ", a, " * gaussian_pdf(x, ",  u, ", ",  o, ") +"
print "             ", a1, " * gaussian_pdf(x, ", u1, ", ", o1, ") +"
print "             ", a2, " * gaussian_pdf(x, ", u2, ", ", o2, ") +"
print "             ", a3, " * gaussian_pdf(x, ", u3, ", ", o3, ");"
